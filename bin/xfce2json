#!/usr/bin/env coffee
fs = require 'fs'
path = require 'path'

main = (argv) ->
  try
    [input, output] = argv
    xfpairs = fs.readFileSync path.resolve(input), 'utf8'
      .split '\n'
      .filter RegExp::test.bind /Foreground|Background|Palette/
      .map (ln) =>
        [key, val] = ln.toLowerCase().split '='
        "#{key.replace 'color', ''}": val
    {foreground, background, palette} = Object.assign ...xfpairs
    final = Object.assign {foreground, background}, palette.split ';'
  catch err
    if not (foreground? and background? and palette?) or err?
      console.error err or 'Invalid colorscheme file'
      process.exit 1
  finally if final?
    if output is '-'
      console.log JSON.stringify final, null, 4
    else
      fs.writeFileSync path.resolve(output), JSON.stringify(final, null, 4)
      console.log "Successfully output to file: '#{output}'"

help = ->
  t = '  '
  console.log """
xfce2json - 1.0.0
#{t}Converts xfce4-terminal color scheme to (termcolors compatible) JSON

USAGE:
#{t}Output to a file
#{t}$ xfce2json <input> [output]

#{t}Output to terminal(stdout)
#{t}$ xfce2json <input> -

#{t}Dispaly this message
#{t}$ xfce2json --help
#{t}$ xfce2json -h

Example with termcolors:
#{t}Convert xfce4 colorscheme to termite colorscheme
#{t}$ xfce2json ./sometheme.theme - | \\
#{t}  termcolors -i json -o termite | \\
#{t}  tee .config/termite/config"""

if module is require.main
  argv = process.argv.slice 2
  if argv.length < 2 and ('-h' in argv or '--help' in argv)
    do help
    process.exit 0
  else if argv.length isnt 2
    console.error 'Not enough arguments'
    console.log 'Pass -h or --help for usage information'
    process.exit 1
  main argv

